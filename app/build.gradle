apply plugin: 'com.android.application'
apply from: '../base.gradle'
apply plugin: 'kotlin-kapt'
apply plugin: 'apk-change-name'

android {
    defaultConfig {
        applicationId project.APPLICATION_ID
        versionCode 1
        versionName "1.0"
        //设置支持的SO库架构
        ndk {
            abiFilters "armeabi-v7a", "x86"
        }
        //只支持 中文简体
        resConfigs "zh-rCN"
    }
    //将res文件夹中的资源以模块的形式分类
    sourceSets {
        main {
            res.srcDirs = [
                    'src/main/res/main',
                    'src/main/res/ui/anim',
                    'src/main/res/ui/appbarlayout',
                    'src/main/res/ui/banner',
                    'src/main/res/ui/card',
                    'src/main/res/ui/constraint',
                    'src/main/res/ui/download',
                    'src/main/res/ui/image',
                    'src/main/res/ui/imagescaletype',
                    'src/main/res/ui/loop',
                    'src/main/res/ui/network',
                    'src/main/res/ui/partition',
                    'src/main/res/ui/refresh',
                    'src/main/res/ui/social',
                    'src/main/res/ui/viewswitcher',
                    'src/main/res/ui/webview',
                    'src/main/res/ui/youtu',
            ]
        }
        debug {
            java.srcDirs = ['src/debug/java']
            res.srcDirs = [
                    'src/debug/res/fps',
                    'src/debug/res'
            ]
        }
        release {
            java.srcDirs = ['src/release/java']
        }
    }
    //lint配置
    lintOptions {
        //true：检测出错时停止构建
        abortOnError true
    }
}

//配置assemble任务依赖lint，编译时就会执行Lint检查
android.applicationVariants.all { variant ->
    variant.outputs.each { output ->
        def lintTask = tasks["lint${variant.name.capitalize()}"]
        output.assemble.dependsOn lintTask
    }
}

dependencies {
    //library
    rootProject.ext.libs.each { lib ->
        switch (lib.key) {
            case "compiler":
                lib.value.each { item ->
                    kapt item.value
                }
                break
            case "debug":
                lib.value.each { item ->
                    debugImplementation item.value
                }
                break
            case "test":
            case "compileOnly":
                break
            default:
                lib.each { item ->
                    implementation item.value
                }
        }
    }
    //module
    rootProject.ext.module.each { lib ->
        switch (lib.key) {
            case "compiler":
                lib.value.each { item ->
                    kapt item.value
                }
                break
            case "lint":
                break
            default:
                lib.each { item ->
                    implementation item.value
                }
        }
    }
}