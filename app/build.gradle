apply plugin: 'com.android.application'
apply from: '../base.gradle'
apply plugin: 'kotlin-kapt'
apply plugin: 'apk-change-name'

android {
    defaultConfig {
        applicationId project.APPLICATION_ID
        versionCode 1
        versionName "1.0"
    }
    //将res文件夹中的资源以模块的形式分类
    sourceSets {
        main {
            res.srcDirs = [
                    'src/main/res/anim',
                    'src/main/res/appbarlayout',
                    'src/main/res/download',
                    'src/main/res/webview',
                    'src/main/res/imagescaletype',
                    'src/main/res/viewswitcher',
                    'src/main/res/main',
                    'src/main/res'
            ]
        }
    }
    //lint配置
    lintOptions {
        //true：检测出错时停止构建
        abortOnError true
    }
}

//配置assemble任务依赖lint，编译时就会执行Lint检查
android.applicationVariants.all { variant ->
    variant.outputs.each { output ->
        def lintTask = tasks["lint${variant.name.capitalize()}"]
        output.assemble.dependsOn lintTask
    }
}

dependencies {
    //https://developer.android.com/guide/topics/ui/layout/recyclerview
    //https://developer.android.com/jetpack/androidx/releases/recyclerview
    implementation "androidx.recyclerview:recyclerview:$recyclerview_version"
    //https://developer.android.com/topic/libraries/architecture/room
    implementation "androidx.room:room-runtime:$room_version"
    kapt "androidx.room:room-compiler:$room_version"
    //https://github.com/InsertKoinIO/koin
    implementation "org.koin:koin-androidx-viewmodel:$koin_version"
    //https://developer.android.com/topic/libraries/architecture/workmanager
    implementation "androidx.work:work-runtime-ktx:$work_version"
    //编译时注解
    implementation project(":annotation")
    kapt project(":compiler")
    //业务通用模块
    implementation project(":common")
}